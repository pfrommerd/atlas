use ordered_float::NotNan;

use crate::parse::ast::{
    Literal,
    Expr, Pattern, Parameter, Arg, LetBinding, ListItemPattern,
    Field, 
    Declaration, Declarations,
    Span, ByteIndex,
    ReplInput
};

use crate::value::storage::ObjPointer;

use crate::parse::lexer::{
    Token,
    StringLiteral, LexicalError
};

grammar<'input>;

extern {
    type Error = LexicalError;
    type Location = ByteIndex;
    enum Token<'input> {
        "doc" => Token::Doc(<&'input str>),
        "identifier" => Token::Identifier(<&'input str>),
        "constructor" => Token::Constructor(<&'input str>),
        "macro" => Token::Macro(<&'input str>),
        "operator" => Token::Operator(<&'input str>),
        "unary operator" => Token::UnaryOperator(<&'input str>),
        "-" => Token::Minus, // special because both operator/unary operator

        "string literal" => Token::StringLiteral(<StringLiteral<'input>>),
        "char literal" => Token::CharLiteral(<char>),

        "int literal" => Token::IntLiteral(<i64>),
        "float literal" => Token::FloatLiteral(<NotNan<f64>>),

        "bool literal" => Token::BoolLiteral(<bool>),

        "let" => Token::Let,
        "in" => Token::In,
        "and" => Token::And,

        "pub" => Token::Pub,

        "fn" => Token::Fn,

        "match" => Token::Match,
        "with" => Token::With,

        "if" => Token::If,
        "then" => Token::Then,
        "else" => Token::Else,

        ":" => Token::Colon,
        ";" => Token::Semicolon,
        "::" => Token::DoubleColon,
        "," => Token::Comma,

        "." => Token::Dot,
        "*" => Token::Star,
        "**" => Token::StarStar,
        "***" => Token::StarStarStar,
        "$" => Token::Cash,
        "#" => Token::Hash,
        "=>" => Token::MatchTo,
        "=" => Token::Equals,
        "|" => Token::Pipe,
        "->" => Token::RArrow,
        "<-" => Token::LArrow,
        "?" => Token::Question,
        "~" => Token::Tilde,
        "@" => Token::At,
        "&" => Token::Ampersand,

        "_" => Token::Underscore,

        "(" => Token::LParen,
        ")" => Token::RParen,

        "{" => Token::LBrace,
        "}" => Token::RBrace,

        "{{" => Token::LDoubleBrace,
        "}}" => Token::RDoubleBrace,

        "[" => Token::LBracket,
        "]" => Token::RBracket,

        "begin" => Token::Begin,
        "end" => Token::End
    }
}

// General utilities

// Can have 0 elements/1 element 0 separators
Sep<Rule, Separator>: Vec<Rule> =
    <mut rules: (<Rule> Separator)*> <last: Rule?> => {
        match last {
        Some(l) => rules.push(l),
        None => ()
        };
        rules
    };

// Does not allow trailing separator
SepStrict<Rule, Separator>: Vec<Rule> =
    <mut rules: (<Rule> Separator)*> <last: Rule> => {
        rules.push(last);
        rules
    };

// requires at least one separator
SepMulti<Rule, Separator> : Vec<Rule> =
    <mut rules: (<Rule> Separator)+> <last: Rule?> => {
        match last {
        Some(l) => rules.push(l),
        None => ()
        };
        rules
    };

// 1-any number of elements,
// trailing comma allowed
// i.e for lists [a], [a,], [a,b], [a,b,]
Comma<Rule>: Vec<Rule> = {
    Sep<Rule, ",">
};

CommaStrict<Rule>: Vec<Rule> = {
    SepStrict<Rule, ",">
};

// at least 1 comma required,
// i.e a,
CommaMulti<Rule>: Vec<Rule> = {
    SepMulti<Rule, ",">
};

// identifiers and constructors are the same thing
// everywhere but for patterns, where a is an identifier
// and Foo is a constructor
Identifier : &'input str = {
    "identifier" => <>,
    "constructor" => <>
}

Literal: Literal = {
    "string literal" => Literal::String(<>.unescape()),
    "char literal" => Literal::Char(<>),
    "int literal" => Literal::Int(<>),
    "float literal" => Literal::Float(<>),
    "bool literal" => Literal::Bool(<>)
}

ListItemPattern : ListItemPattern<'input> = {
    <l:@L> "**" <ident:"identifier"?> <r:@R> => 
        ListItemPattern::Expansion(Span::new(l, r), ident),
    <l:@L> <pattern:Pattern> <r:@R> => 
        ListItemPattern::Simple(Span::new(l, r), pattern)
}

ConstructorPatternArgs : Vec<Pattern<'input>> = {
    "(" <params:Comma<Pattern>> ")" => params
}

Pattern : Pattern<'input> = {
    <l:@L> <ident:"constructor"> <params:ConstructorPatternArgs?> <r:@R> =>
        match params {
            None => Pattern::Variant(Span::new(l, r), ident, Vec::new()),
            Some(ps) => Pattern::Variant(Span::new(l, r), ident, ps)
        },
    <l:@L> "_" <r:@R> =>
        Pattern::Hole(Span::new(l, r)),
    // Note: cannot bind identifier
    // expect as directionary keys using shorthand notation
    // i.e let {Bar} = {Bar: f}
    // will bind Bar to f
    // let Bar = f
    // will do a pattern match on f with variant case Bar!
    <l:@L> <ident:"identifier"> <r:@R> =>
        Pattern::Identifier(Span::new(l, r), ident),
    <l:@L> <lit:Literal> <r:@R> => Pattern::Literal(Span::new(l, r), lit),
    <l:@L> "[" <items:Comma<ListItemPattern>> "]" <r:@R> => Pattern::List(Span::new(l, r), items)
}

// Expressions
Parameter : Parameter<'input> = {
    <l:@L> <ident:"identifier"> <r:@R> =>
        Parameter::Named(Span::new(l, r), ident),
    <l:@L> "**" <ident:"identifier"?> <r:@R> =>
        Parameter::VarPos(Span::new(l, r), ident),
    <l:@L> "***" <ident:"identifier"?> <r:@R> =>
        Parameter::VarKeys(Span::new(l, r), ident),
    <l:@L> <ident:"identifier"> "?" <r:@R> =>
        Parameter::Optional(Span::new(l, r), ident)
}

Arg : Arg<'input> = {
    <l:@L> <e:Expr> <r:@R> => 
        Arg::Pos(Span::new(l, r), e),
    <l:@L> <ident:"identifier"> ":" <e:Expr> <r:@R> => 
        Arg::ByName(Span::new(l, r), ident, e),
}

ShorthandField : Field<'input> = {
    <l:@L> <ident:Identifier> <r:@R> => 
        Field::Shorthand(Span::new(l, r), ident),
}

SimpleField : Field<'input> = {
    <l:@L> <ident:Identifier> <m:@R> ":" <e:Expr>  =>
        Field::Simple(Span::new(l, m), ident, e)
}

ExpandField : Field<'input> = {
    <l:@L> "***" <e:Expr> <r:@R>  =>
        Field::Expansion(Span::new(l, r), e)
}

Field : Field<'input> = {
    ShorthandField, SimpleField, ExpandField
}

// a @ (| x , y | (x  @ 1))
// 1 + | x, y | x + y

Record : Expr<'input> = {
    // Braces are complicated since { a } could be either a record
    // or a scope. Therefore we require record literals starting with Default
    // fields to include a comma i.e { a } is a scope and { a, } is a record
    <l:@L> "{" "}" <r:@R> => {
        Expr::Record(Span::new(l, r), Vec::new())
    },
    <l:@L> "{" "," "}" <r:@R> => {
        Expr::Record(Span::new(l, r), Vec::new())
    },
    <l:@L> "{" <first:SimpleField> <mut fields:("," <Field>)*> ","? "}" <r:@R> => {
        fields.insert(0, first);
        Expr::Record(Span::new(l, r), fields)
    },
    <l:@L> "{" <first:ExpandField> <mut fields:("," <Field>)*> ","? "}" <r:@R> => {
        fields.insert(0, first);
        Expr::Record(Span::new(l, r), fields)
    },
    <l:@L> "{" <first:Identifier> <m:@R> "," <mut fields:Comma<Field>> "}" <r:@R> => {
        fields.insert(0, Field::Shorthand(Span::new(l, m), first));
        Expr::Record(Span::new(l, r), fields)
    },
}

Scope : Expr<'input> = {
    <l:@L> "{" <ld:@L> <d:Declaration*> <rd:@R> <e:Expr?> "}" <r:@R> => 
        Expr::Scope(Span::new(l, r), Declarations::new(Span::new(ld, rd), d), 
            Box::new(e))
}

NonEmptyScope : Expr<'input> = {
    <l:@L> "{" <ld:@L> <d:Declaration*> <rd:@R> <e:Expr> "}" <r:@R> => 
        Expr::Scope(Span::new(l, r), Declarations::new(Span::new(ld, rd), d), 
            Box::new(Some(e)))
}

IfElse : Expr<'input> = {
    <l:@L> "if" <cond: Expr> <suc: Scope> <elif:("else" "if" Expr Scope)+> <last:("else" Scope)?> <r:@R> => {
        let mut else_clause = match last {
            Some((_, s)) => Some(Box::new(s)),
            None => None
        };
        for (_, _, c, s) in elif.into_iter().rev() {
            else_clause = Some(Box::new(Expr::IfElse(Span::new(l, r), Box::new(c), Box::new(s), else_clause)));
        }
        Expr::IfElse(Span::new(l, r), Box::new(cond), Box::new(suc), else_clause)
    },
}



MatchCase : (Pattern<'input>, Expr<'input>) = {
    <p:Pattern> "=>" <e:Expr> => (p, e)
}

Match : Expr<'input> = {
    <l:@L> "match" <scrutinized:Expr> "with" "{" <cases: Comma<MatchCase>> "}" <r:@R> => 
        Expr::Match(Span::new(l, r), Box::new(scrutinized), cases)
}

Builtin : Expr<'input> = {
    <l:@L> "$" <iden:Identifier> "(" <entries:Comma<Expr>> ")" <r:@R> =>
        Expr::Builtin(Span::new(l, r), iden, entries)
}

// All closed construction expressions
AtomicExpr : Expr<'input> = {
    <l:@L> <ident:Identifier> <r:@R> => Expr::Identifier(Span::new(l, r), ident),
    <l:@L> <lit:Literal> <r:@R> => Expr::Literal(Span::new(l, r), lit),
    <l:@L> "(" ")" <r:@R> => Expr::Literal(Span::new(l, r), Literal::Unit),
    <l:@L> "[" <entries:Comma<Expr>> "]" <r:@R> =>
        Expr::List(Span::new(l, r), entries),

    // both . and :: project
    <l:@L> <e:AtomicExpr> "." <ident:Identifier> <r:@R> =>
        Expr::Project(Span::new(l, r), Box::new(e), ident),
    <l:@L> <e:AtomicExpr> "::" <ident:Identifier> <r:@R> =>
        Expr::Project(Span::new(l, r), Box::new(e), ident),

    // function calls (both () versions and {} version)
    <l:@L> <lam:AtomicExpr> "(" <args:Comma<Arg>> ")" <r:@R> =>
        Expr::Call(Span::new(l, r), Box::new(lam), args),
    // closed anonymous functions
    // fn(a, b) {}
    // this allows for 0-argument functions, whereas the |x| syntax does not
    // since || is the "or" operator
    <l:@L> "fn" "(" <params:Comma<Parameter>> ")" <s:Scope> <r:@R> =>
        Expr::Lambda(Span::new(l, r), params, Box::new(s)),

    Record,
    NonEmptyScope, // must be non-empty to not be confused with a record {}
    IfElse,
    Match,
    Builtin,

    // if there is a comma it is a tuple, i.e  (1 + 1,) vs (1 + 1)
    <l:@L> "(" <parts:CommaMulti<Expr>> ")" <r:@R> => 
        Expr::Tuple(Span::new(l, r), parts),
    <l:@L> "(" "," ")" <r:@R> => 
        Expr::Tuple(Span::new(l, r), Vec::new()),
    // wraping an expression in () 
    "(" <exp: Expr> ")" => exp
}

// Anonymous functions
OpenLamExpr : Expr<'input> = {
    <l:@L> "|" <params: Comma<Parameter>> "|" <e: Expr> <r:@R> =>
        Expr::Lambda(Span::new(l, r), params, Box::new(e))
}

PrefixOperator : &'input str = {
    "unary operator" => <>,
    "-" => "-"
}

PrefixExpr : Expr<'input> = {
    AtomicExpr,
    <l:@L> <op:PrefixOperator> <arg:PrefixExpr> <r:@R> =>
        Expr::Prefix(Span::new(l, r), op, Box::new(arg))
}

// To prevent ambiguities this is a lambda
// expression with some number of prefixes.
// an Infix expression can that way only handle a lambda
// expression if it is at the end
OpenLamPrefixExpr : Expr<'input> = {
    OpenLamExpr,
    <l:@L> <op:PrefixOperator> <arg:OpenLamPrefixExpr> <r:@R> =>
        Expr::Prefix(Span::new(l, r), op, Box::new(arg))
}

// a binary operator can |, -
// in addition to things the lexer calls an "operator"
// @ is handled separately since there we need to allow
// named args, etc
InfixOperator : &'input str = {
    "|" => "|",
    "-" => "-",
    "operator" => <>
}

InfixExpr : Expr<'input> = {
    <l:@L> <parts:(PrefixExpr InfixOperator)+> <last:PrefixExpr> <r:@R> => {
        let mut args = Vec::new();
        let mut ops = Vec::new();
        for (exp, op) in parts {
            args.push(exp);
            ops.push(op);
        }
        args.push(last);
        Expr::Infix(Span::new(l, r), args, ops)
    },
    <l:@L> <parts:(PrefixExpr InfixOperator)+> <last:OpenLamPrefixExpr> <r:@R> => {
        let mut args = Vec::new();
        let mut ops = Vec::new();
        for (exp, op) in parts {
            args.push(exp);
            ops.push(op);
        }
        args.push(last);
        Expr::Infix(Span::new(l, r), args, ops)
    }
}

pub Expr : Expr<'input> = {
    InfixExpr,
    PrefixExpr,
    OpenLamPrefixExpr
}

LetBinding: LetBinding<'input> = {
    <pat: Pattern> "=" <e:Expr> => LetBinding::new((pat, e))
}

Annotations : Vec<&'input str> = {
    "#" "[" <annotations: Comma<Identifier>> "]" => annotations
}

Declaration : Declaration<'input> = {
    <l:@L> "let" <bindings: LetBinding> <r:@R> ";" =>
        Declaration::LetDeclare(Span::new(l, r), false, bindings),
    <l:@L> <annotations: Annotations?> "fn" <ident: "identifier"> "(" <params:Comma<Parameter>> ")" <s:Scope> <r:@R> =>
        Declaration::FnDeclare(Span::new(l, r), false, ident, params, s, annotations),
    <l:@L> <annotations: Annotations?> "fn" "(" <op: InfixOperator> ")" "(" <params:Comma<Parameter>> ")" <s:Scope> <r:@R> =>
        Declaration::FnDeclare(Span::new(l, r), false, op, params, s, annotations)
}

// Top level declarations allow let without a semicolon
// since in scope blocks we need to differentiate
// let foo = Bar(baz, blah)
// and
// let foo = Bar;
// (baz, blah)
// but we don't need to do that for top-level
// declarations, so the ; is optional
TLDeclaration : Declaration<'input> = {
    <d:Declaration> => d,
    // Allow let declaration without a ;
    <l:@L> "let" <bindings: LetBinding> <r:@R> =>
        Declaration::LetDeclare(Span::new(l, r), false, bindings),
    "pub" <l:@L> "let" <bindings: LetBinding> <r:@R> =>
        Declaration::LetDeclare(Span::new(l, r), true, bindings),

    "pub" <mut d:Declaration> => {
        d.set_public(true);
        d
    }
}

pub Module : Expr<'input> = {
    <l:@L> <declarations: TLDeclaration*> <r:@R> => {
        Expr::Module(Declarations::new(Span::new(l, r), declarations))
    }
}


Pointer : Some(u64) = {
    "&" <addr:"int literal"> => {
        let res : Result<u64, _> = addr.try_into();
        match res {
            Ok(a) => a
            _ => 0
        }
    }
}


pub ReplInput : ReplInput<'input> = {
    <decl: TLDeclaration> => ReplInput::Decl(decl),
    <exp: Expr> => ReplInput::Expr(exp),
    <ptr: Pointer> => ReplInput::Pointer(ptr),
}